we%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%  Logtalk - Object oriented extension to Prolog
%  Release 2.9.0
%
%  configuration file for Quintus Prolog 3.3
%
%  last updated: August 24, 2000
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


:- [library(date)].



%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%  ISO Prolog Standard predicates that we must define because they are
%  not built-in
%
%  add a clause for lgt_iso_predicate/1 declaring each ISO predicate that
%  you must define; there must be at least one clause for this predicate
%  whose call should fail if you don't define any ISO predicates
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


:- op(700, xfx, \=).


% lgt_iso_predicate(?callable).

lgt_iso_predicate(_ \= _).
lgt_iso_predicate(atom_codes(_, _)).
lgt_iso_predicate(atom_concat(_, _, _)).
lgt_iso_predicate(catch(_, _, _)).
lgt_iso_predicate(number_codes(_, _)).
lgt_iso_predicate(once(_)).
lgt_iso_predicate(throw(_)).


Term1 \= Term2 :-
	\+ (Term1 = Term2).


atom_codes(Atom, Codes) :-
	atom_chars(Atom, Codes).


atom_concat(Atom1, Atom2, Atom3) :-
	nonvar(Atom1), nonvar(Atom2),
	!,
	atom_chars(Atom1, Codes1),
	atom_chars(Atom2, Codes2),
	lgt_append(Codes1, Codes2, Codes3),
	atom_chars(Atom3, Codes3).

atom_concat(Atom1, Atom2, Atom3) :-
	nonvar(Atom3),
	!,
	atom_chars(Atom3, Codes3),
	lgt_append(Codes1, Codes2, Codes3),
	atom_chars(Atom1, Codes1),
	atom_chars(Atom2, Codes2).


catch(Goal, Catcher, Recovery) :-
	on_exception(Catcher, Goal, Recovery).


number_codes(Number, Codes) :-
	number_chars(Number, Codes).


once(Goal) :-
	call(Goal),
	!.


throw(Ball) :-
	raise_exception(Ball).



%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%  predicate properties
%
%  this predicate must return at least static, dynamic and built_in 
%  properties for an existing predicate
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


% lgt_predicate_property(+callable, ?predicate_property)

lgt_predicate_property(Pred, Prop) :-
	predicate_property(Pred, Prop).



%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%  metapredicates
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


% forall(+callable, +callble)

forall(Generate, Test) :-
	\+ call((Generate, \+ call(Test))).


% lgt_call/2-8
%
% use these definitions only if your compiler does
% not provide call/1-8 as built-in predicates

lgt_call(F, A) :-
	Call =.. [F, A],
	call(Call).


lgt_call(F, A1, A2) :-
	Call =.. [F, A1, A2],
	call(Call).


lgt_call(F, A1, A2, A3) :-
	Call =.. [F, A1, A2, A3],
	call(Call).


lgt_call(F, A1, A2, A3, A4) :-
	Call =.. [F, A1, A2, A3, A4],
	call(Call).


lgt_call(F, A1, A2, A3, A4, A5) :-
	Call =.. [F, A1, A2, A3, A4, A5],
	call(Call).


lgt_call(F, A1, A2, A3, A4, A5, A6) :-
	Call =.. [F, A1, A2, A3, A4, A5, A6],
	call(Call).


lgt_call(F, A1, A2, A3, A4, A5, A6, A7) :-
	Call =.. [F, A1, A2, A3, A4, A5, A6, A7],
	call(Call).


% lgt_once/2-8
%
% if your compiler provides call/1-8 as built-in 
% predicates rewrite these definitions using call(...), !.

lgt_once(F, A) :-
	Call =.. [F, A],
	once(Call).


lgt_once(F, A1, A2) :-
	Call =.. [F, A1, A2],
	once(Call).


lgt_once(F, A1, A2, A3) :-
	Call =.. [F, A1, A2, A3],
	once(Call).


lgt_once(F, A1, A2, A3, A4) :-
	Call =.. [F, A1, A2, A3, A4],
	once(Call).


lgt_once(F, A1, A2, A3, A4, A5) :-
	Call =.. [F, A1, A2, A3, A4, A5],
	once(Call).


lgt_once(F, A1, A2, A3, A4, A5, A6) :-
	Call =.. [F, A1, A2, A3, A4, A5, A6],
	once(Call).


lgt_once(F, A1, A2, A3, A4, A5, A6, A7) :-
	Call =.. [F, A1, A2, A3, A4, A5, A6, A7],
	once(Call).



%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%  file extension predicates
%
%  these extensions are used by Logtalk load/compile predicates
%
%  you may want to change the extension for Prolog files to match 
%  the one expected by your Prolog compiler
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


% lgt_file_extension(?atom, ?atom)

lgt_file_extension(logtalk, '.lgt').
lgt_file_extension(prolog, '.pl').
lgt_file_extension(xml, '.xml').



%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%  default compiler options
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


% lgt_default_compiler_option(?atom, ?atom)
%
% default values for all compiler options

lgt_default_compiler_option(iso_initialization_dir, true).

lgt_default_compiler_option(xml, on).
lgt_default_compiler_option(xsl, 'lgtxml.xsl').

lgt_default_compiler_option(unknown, warning).
lgt_default_compiler_option(misspelt, warning).
lgt_default_compiler_option(singletons, warning).
lgt_default_compiler_option(lgtredef, warning).
lgt_default_compiler_option(plredef, silent).

lgt_default_compiler_option(report, on).



%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%  list predicates
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


lgt_append([], List, List).
lgt_append([Head| Tail], List, [Head| Tail2]) :-
	lgt_append(Tail, List, Tail2).


lgt_member(Head, [Head| _]).
lgt_member(Head, [_| Tail]) :-
	lgt_member(Head, Tail).


lgt_member_var(V, [H| _]) :-
	V == H.
lgt_member_var(V, [_| T]) :-
	lgt_member_var(V, T).


lgt_proper_list([]).
lgt_proper_list([_| List]) :-
	lgt_proper_list(List).



%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%  file predicates
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


% lgt_file_exists(+atom)
%
% see if a file exist in the current directory

lgt_file_exists(File) :-
	absolute_file_name(File, [access(exist), file_errors(fail)], _).


% lgt_load_prolog_code(+atom)
%
% compile and load a Prolog file

lgt_load_prolog_code(File) :-
	compile(File).



%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%  sorting predicates
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


% lgt_keysort(+list, -list)

lgt_keysort(List, Sorted) :-
	keysort(List, Sorted).


% lgt_sort(+list, -list)

lgt_sort(List, Sorted) :-
	sort(List, Sorted).



%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%  time and date predicates
%
%  if your Prolog compiler does not provide access to the operating system 
%  time and date just write dummy definitions
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


% lgt_current_date(?Year, ?Month, ?Day)

lgt_current_date(Year, Month, Day) :-
	date(date(Day, Month, Year)).


% lgt_current_time(?Hours, ?Mins, ?Secs)

lgt_current_time(Hours, Mins, Secs) :-
	time(time(Hours, Mins, Secs)).



%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%  timing predicate
%
%  if your Prolog compiler does not provide access to a timing predicate 
%  just write dummy definition
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


% lgt_cpu_time(-Seconds)

lgt_cpu_time(Seconds) :-
	statistics(runtime, [Miliseconds| _]),
	Seconds is Miliseconds / 1000.



%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%  comparison predicate
%
%  the usual compare/3 definition
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


% compare(?atom, @term, @term) -- built-in



%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%  end!
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
