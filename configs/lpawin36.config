%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%  Logtalk - Object oriented extension to Prolog
%  Release 2.9.3
%
%  initialization file for LPA WinProlog 3.61
%
%  last updated: December 30, 2001
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


:- prolog_flag(syntax_errors, _, error).



%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%  ISO Prolog Standard predicates that we must define because they are
%  not built-in
%
%  add a clause for lgt_iso_predicate/1 declaring each ISO predicate that
%  we must define; there must be at least one clause for this predicate
%  whose call should fail if we don't define any ISO predicates
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


:- dynamic(lgt_exception_/1).


% lgt_iso_predicate(?callable).

lgt_iso_predicate(atom_codes(_, _)).
lgt_iso_predicate(atom_concat(_, _, _)).
lgt_iso_predicate(catch(_, _, _)).
lgt_iso_predicate(close(_)).
lgt_iso_predicate(nl(_)).
lgt_iso_predicate(number_codes(_, _)).
lgt_iso_predicate(once(_)).
lgt_iso_predicate(open(_, _, _)).
lgt_iso_predicate(read_term(_, _, _)).
lgt_iso_predicate(throw(_)).
lgt_iso_predicate(write_term(_, _, _)).


atom_codes(Atom, Codes) :-
	atom_chars(Atom, Codes).


atom_concat(Atom1, Atom2, Atom3) :-
	nonvar(Atom1), nonvar(Atom2),
	!,
	name(Atom1, Codes1),
	name(Atom2, Codes2),
	lgt_append(Codes1, Codes2, Codes3),
	name(Atom3, Codes3).

atom_concat(Atom1, Atom2, Atom3) :-
	nonvar(Atom3),
	!,
	name(Atom3, Codes3),
	lgt_append(Codes1, Codes2, Codes3),
	name(Atom1, Codes1),
	name(Atom2, Codes2).


lpa_catch(Error, Goal) :-
	catch(Error, Goal).


:- hide(catch).


catch(Error, Goal) :-
	lpa_catch(Error, Goal).


catch(Goal, Catcher, Recovery) :-
	lpa_catch(Error, Goal),
	(Error = 0 ->
		true
		;
		(Error = -1 ->
			!, fail
			;
			(Error = 999 ->
				retract(lgt_exception_(Ball)),
				!,
				(Catcher = Ball ->
					call(Recovery)
					;
					throw(Ball))
				;
				error_message(Error, Message),
				(Catcher = Message ->
					call(Recovery)
					;
					write(Message), nl, abort)))).


:- hide(close).


close(Stream) :-
	fclose(Stream).


nl(Stream) :-
	output(Current),
	output(Stream),
	nl,
	output(Current).


number_codes(Number, Codes) :-
	number_chars(Number, Codes).


once(Goal) :-
	one(Goal).


open(File, read, File) :-
	fopen(File, File, 0).

open(File, write, File) :-
	fcreate(File, File, 0).


read_term(Stream, Term, [singletons([])]) :-
	!,
	input(Current),
	input(Stream),
	read(Term),
	input(Current).

read_term(Stream, Term, _) :-
	input(Current),
	input(Stream),
	read(Term),
	input(Current).


throw(Error) :-
	asserta(lgt_exception_(Error)),
	throw(999, Error).


write_term(Stream, Term, [quoted(true)]) :-
	!,
	output(Current),
	output(Stream),
	writeq(Term),
	output(Current).

write_term(Stream, Term, _) :-
	output(Current),
	output(Stream),
	write(Term),
	output(Current).



%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%  predicate properties
%
%  this predicate must return at least static, dynamic and built_in 
%  properties for an existing predicate
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


% lgt_predicate_property(+callable, ?predicate_property)

lgt_predicate_property(Pred, Prop) :-
	predicate_property(Pred, Prop).



%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%  metapredicates
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


% lgt_call/2-8
%
% use these definitions only if your compiler does
% not provide call/1-8 as built-in predicates

lgt_call(F, A) :-
	F(A).


lgt_call(F, A1, A2) :-
	F(A1, A2).


lgt_call(F, A1, A2, A3) :-
	F(A1, A2, A3).


lgt_call(F, A1, A2, A3, A4) :-
	F(A1, A2, A3, A4).


lgt_call(F, A1, A2, A3, A4, A5) :-
	F(A1, A2, A3, A4, A5).


lgt_call(F, A1, A2, A3, A4, A5, A6) :-
	F(A1, A2, A3, A4, A5, A6).


lgt_call(F, A1, A2, A3, A4, A5, A6, A7) :-
	F(A1, A2, A3, A4, A5, A6, A7).


% lgt_once/2-8
%
% if your compiler provides call/1-8 as built-in 
% predicates rewrite these definitions using call(...), !.

lgt_once(F, A) :-
	one(F(A)).


lgt_once(F, A1, A2) :-
	one(F(A1, A2)).


lgt_once(F, A1, A2, A3) :-
	one(F(A1, A2, A3)).


lgt_once(F, A1, A2, A3, A4) :-
	one(F(A1, A2, A3, A4)).


lgt_once(F, A1, A2, A3, A4, A5) :-
	one(F(A1, A2, A3, A4, A5)).


lgt_once(F, A1, A2, A3, A4, A5, A6) :-
	one(F(A1, A2, A3, A4, A5, A6)).


lgt_once(F, A1, A2, A3, A4, A5, A6, A7) :-
	one(F(A1, A2, A3, A4, A5, A6, A7)).



%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%  file extension predicates
%
%  these extensions are used by Logtalk load/compile predicates
%
%  you may want to change the extension for Prolog files to match 
%  the one expected by your Prolog compiler
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


% lgt_file_extension(?atom, ?atom)

lgt_file_extension(logtalk, '.lgt').
lgt_file_extension(prolog, '.pl').
lgt_file_extension(xml, '.xml').



%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%  default flag values
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


% lgt_default_flag(?atom, ?atom)
%
% default values for all flags

lgt_default_flag(iso_initialization_dir, true).

lgt_default_flag(xml, on).
lgt_default_flag(xsl, 'lgtxml.xsl').

lgt_default_flag(unknown, warning).
lgt_default_flag(misspelt, warning).
lgt_default_flag(singletons, warning).
lgt_default_flag(lgtredef, warning).
lgt_default_flag(plredef, silent).
lgt_default_flag(portability, silent).

lgt_default_flag(report, on).



%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%  list predicates
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


lgt_append([], List, List).
lgt_append([Head| Tail], List, [Head| Tail2]) :-
	lgt_append(Tail, List, Tail2).


lgt_member(Head, [Head| _]).
lgt_member(Head, [_| Tail]) :-
	lgt_member(Head, Tail).


lgt_member_var(V, [H| _]) :-
	V == H.
lgt_member_var(V, [_| T]) :-
	lgt_member_var(V, T).


lgt_proper_list([]).
lgt_proper_list([_| List]) :-
	lgt_proper_list(List).



%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%  file predicates
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


% lgt_file_exists(+atom)
%
% see if a file exist in the current directory

lgt_file_exists(File) :-
	catch(absolute_file_name(File, [access(exist)], _), _, fail).


% lgt_load_prolog_code(+atom)
%
% compile and load a Prolog file

lgt_load_prolog_code(File) :-
	reconsult(File).



%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%  sorting predicates
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


% lgt_keysort(+list, -list)

lgt_keysort(List, Sorted) :-
	keysort(List, Sorted).


% lgt_sort(+list, -list)

lgt_sort(List, Sorted) :-
	sort(List, Sorted).



%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%  time and date predicates
%
%  if your Prolog compiler does not provide access to the operating system 
%  time and date just write dummy definitions
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


% lgt_current_date(?Year, ?Month, ?Day)

lgt_current_date(Year, Month, Day) :-
	date(Day, Month, Year).


% lgt_current_time(?Hours, ?Mins, ?Secs)

lgt_current_time(Hours, Mins, Secs) :-
	time(Hours, Mins, Secs, _).



%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%  timing predicate
%
%  if your Prolog compiler does not provide access to a timing predicate 
%  just write dummy definition
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


% lgt_cpu_time(-Seconds)

lgt_cpu_time(Seconds) :-
	ticks(Ticks),
	AbsTicks is Ticks mod 2^32,
	Seconds is AbsTicks / 4660.



%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%  comparison predicate
%
%  the usual compare/3 definition
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


% compare(?atom, @term, @term) -- built-in



%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%  end!
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
